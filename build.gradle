import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.70'
    id 'maven-publish'
}

group 'org.cultofclang.minecraft'
version '0.1'

if (System.getenv("GITHUB_RUN_NUMBER")!=null) {
    version += "." + System.getenv("GITHUB_RUN_NUMBER")
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly 'org.bukkit:bukkit:1.15.2-R0.1-SNAPSHOT'
    shadow  "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"
    shadow  "com.charleskorn.kaml:kaml:0.15.0"

    shadow  "org.jetbrains.exposed:exposed-core:0.22.1"
    shadow  "org.jetbrains.exposed:exposed-dao:0.22.1"
    shadow  "org.jetbrains.exposed:exposed-jdbc:0.22.1"

    shadow  "org.xerial:sqlite-jdbc:3.30.1"
    shadow  "org.slf4j:slf4j-api:1.7.30"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/CultOfClang/Minecraft/")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

if (project.hasProperty("plugin_path") && plugin_path) {
    task copyJar(type: Copy) {
        from shadowJar
        into plugin_path
    }
    shadowJar.finalizedBy(copyJar)
}
